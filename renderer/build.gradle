/*
 * Copyright 2018, Oath Inc.
 * Licensed under the terms of the MIT License. See LICENSE.md file in project root for terms.
 */

ext {
    // Android build config constants
    BUILD_CONFIG_CONSTS = [
            'FLAT_RENDERER'       : "com.onemobilesdk.videorenderer.flat@${GIT_BASED_VERSION}",
            'THREE_SIXTY_RENDERER': "com.onemobilesdk.videorenderer.360@${GIT_BASED_VERSION}",
            'FISH_EYE_RENDERER'   : "com.onemobilesdk.videorenderer.fisheye@${GIT_BASED_VERSION}"
    ]

    // Javadoc configuration
    JAVADOC_TITLE = "OneMobileSDK stream renderer javadoc"

    // Maven
    GROUP_ID = 'com.aol.one.publishers.android'
    ARTIFACT_ID = 'renderer'
    LIBRARY_VERSION = GIT_BASED_VERSION

    // ExoPlayer
    EXO_VERSION = '2.6.1'

    MINIFY_LIB = true
}

apply from: 'https://raw.githubusercontent.com/aol-public/OneMobileSDK-tools-android/master/android.gradle'
apply from: 'https://raw.githubusercontent.com/aol-public/OneMobileSDK-tools-android/master/javadoc.gradle'
apply from: 'https://raw.githubusercontent.com/aol-public/OneMobileSDK-tools-android/master/sources.gradle'
apply from: 'https://raw.githubusercontent.com/aol-public/OneMobileSDK-tools-android/master/github-maven.gradle'
apply plugin: 'api-tracker'

apiTracker {
    compareVersion = GIT_TAG
    reportFile = file("$buildDir/maven/API CHANGES/$ARTIFACT_ID/$compareVersion to ${LIBRARY_VERSION}.md")
    implicitNamespaces = ["com.aol.mobile.sdk.player", "java.util"]
}

afterEvaluate {
    gitPublishCommit.dependsOn checkApiChanges
    checkApiChanges.dependsOn gitPublishReset
}

dependencies {
    compileOnly "com.android.support:support-annotations:$SUPPORT_VERSION"
    compileOnly "com.android.support:support-v4:$SUPPORT_VERSION"
    compileOnly "com.aol.one.publishers.android:chromecast:1.7"

    implementation "com.google.android.exoplayer:exoplayer-core:$EXO_VERSION"
    implementation "com.google.android.exoplayer:exoplayer-hls:$EXO_VERSION"

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$KOTLIN_VERSION"
    testImplementation "org.assertj:assertj-core:3.9.0"
    testImplementation "org.mockito:mockito-core:2.12.0"
}
