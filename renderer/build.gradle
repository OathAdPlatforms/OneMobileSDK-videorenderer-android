import java.util.concurrent.TimeUnit

/*
 * Copyright 2018, Oath Inc.
 * Licensed under the terms of the MIT License. See LICENSE.md file in project root for terms.
 */

apply plugin: 'android-ci-library'

androidCi {
    groupId = 'com.aol.one.publishers.android'
    artifactId = 'renderer'
    githubRepo = 'OathAdPlatforms/OneMobileSDK-releases-android'
    apiTrimNamespaces.add("com.aol.mobile.sdk.renderer")
}

String libraryVersion = calculateVersions()

private static int waitOnProcess(Process process) {
    process.waitFor(60, TimeUnit.SECONDS)
    return process.exitValue()
}

private String calculateVersions() {
    Boolean isCommitTagged
    Process tagProcess = "git describe --exact-match --tags HEAD".execute()
    isCommitTagged = waitOnProcess(tagProcess) == 0

    String gitTag = ""
    def process = "git describe --tags --abbrev=0".execute()

    process.in.eachLine { line ->
        gitTag += line
    }

    if (gitTag == "" || gitTag == null) {
        gitTag = "0.1"
    }

    List<String> versionParts = gitTag.split(".")
    String nextVersion
    switch (versionParts.size()) {
        case 0:
            nextVersion = "1.0"
            break
        case 1:
            nextVersion = "1.0"
            break
        default:
            nextVersion = versionParts.withIndex().collect { String part, int index ->
                if (index == (versionParts.size() - 1)) {
                    "${part.toInteger() + 1}"
                } else {
                    part
                }
            }.joinToString(separator = ".", postfix = "", prefix = "")
    }

    if (isCommitTagged) {
        return gitTag
    } else {
        return "$nextVersion-SNAPSHOT"
    }
}

android {
    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        versionCode = 1
        versionName = libraryVersion

        buildConfigField "String", 'FLAT_RENDERER', "\"com.onemobilesdk.videorenderer.flat@$version\""
        buildConfigField "String", 'THREE_SIXTY_RENDERER', "\"com.onemobilesdk.videorenderer.360@$version\""
        buildConfigField "String", 'FISH_EYE_RENDERER', "\"com.onemobilesdk.videorenderer.fisheye@$version\""

        manifestPlaceholders = [
                'FLAT_RENDERER'       : "com.onemobilesdk.videorenderer.flat@$version",
                'THREE_SIXTY_RENDERER': "com.onemobilesdk.videorenderer.360@$version",
                'FISH_EYE_RENDERER'   : "com.onemobilesdk.videorenderer.fisheye@$version"
        ]
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compileOnly "com.android.support:support-annotations:$support_version"
    compileOnly "com.android.support:support-v4:$support_version"

    implementation "com.google.android.exoplayer:exoplayer-core:$exo_version"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exo_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exo_version"

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.assertj:assertj-core:3.9.1"
    testImplementation "org.mockito:mockito-core:2.16.0"
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'com.android.support') {
                details.useVersion support_version
            }
        }
    }
}